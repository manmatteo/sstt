open Core
open Sstt_utils

type delta = {v : VarSet.t; f : FieldVarSet.t; r : RowVarSet.t}
module type VarSettings = sig
  val tcompare : Var.t -> Var.t -> int
  val fcompare : FieldVar.t -> FieldVar.t -> int
  val rcompare : RowVar.t -> RowVar.t -> int
  val delta : delta
end

(* module type LabelOrder = sig
  val compare : Label.t -> Label.t -> int
end *)

(* module Row = struct
  type t = Records.t * LabelSet.t
end *)

type constr = Ty.t * Ty.t


module Make(VS:VarSettings) = struct

  exception Unsat

  module Var = struct
    include Var
    (* prevent from using default comparison*)
    let equal (_:t) (_:t) = () [@@ocaml.warning "-32"]
    let compare (_:t) (_:t) = () [@@ocaml.warning "-32"]
  end

  module type Constraint = sig
    type var
    module VSet : Set.S with type elt = var
    module T : sig
      type t
      val vars : t -> VSet.t
      val leq : t -> t -> bool
      val cup : t -> t -> t
      val cap : t -> t -> t
    end
    type t = T.t * var * T.t
    val delta : VSet.t
    val subsumes : t -> t -> bool
    val compare : t -> t -> int
    val vcompare : t -> t -> int
  end

  module TConstr : Constraint
    with type VSet.t = VarSet.t
    and type T.t  = Ty.t
    and type var = Var.t
  = struct
    type var = Var.t
    module T = Ty
    type t = Ty.t * Var.t * Ty.t (* s ≤ α ≤ t *)

    module VSet = VarSet
    let delta = VS.delta.v

    (* C1 subsumes C2 if it has the same variable
       and gives better bounds (larger lower bound and smaller upper bound)
    *)
    let subsumes (t1, v1, t1') (t2, v2, t2') =
      VS.tcompare v1 v2 = 0 &&
      Ty.leq t2 t1 && Ty.leq t1' t2'

    let compare (t1,v1,t1') (t2,v2,t2')=
      VS.tcompare v1 v2 |> ccmp
        Ty.compare t1 t2 |> ccmp
        Ty.compare t1' t2'

    let vcompare (_,v1,_) (_,v2,_) = VS.tcompare v1 v2
  end

  module FConstr : Constraint
    with type VSet.t = FieldVarSet.t
    and type T.t  = Ty.F.t
    and type var = FieldVar.t
  = struct
    type var = FieldVar.t
    module T = struct 
      include Ty.F
      let vars = fvars
    end
    type t = Ty.F.t * FieldVar.t * Ty.F.t

    module VSet = FieldVarSet

    let delta = VS.delta.f

    let subsumes (t1, v1, t1') (t2, v2, t2') =
      VS.fcompare v1 v2 = 0 &&
      Ty.F.leq t2 t1 && Ty.F.leq t1' t2'

    let compare (t1,v1,t1') (t2,v2,t2')=
      VS.fcompare v1 v2 |> ccmp
        Ty.F.compare t1 t2 |> ccmp
        Ty.F.compare t1' t2'
    
    let vcompare (_,v1,_) (_,v2,_) = VS.fcompare v1 v2
  end

  (* module RConstr : Constraint with type var := RowVar.t = struct
    type t = Row.t * RowVar.t * Row.t (* s ≤ α ≤ t *)
    module VSet = RowVarSet

    let subsumes (t1, v1, t1') (t2, v2, t2') =
      VO.rcompare v1 v2 = 0 &&
      Ty.leq t2 t1 && Ty.leq t1' t2'

    let compare (t1,v1,t1') (t2,v2,t2')=
      VO.rcompare v1 v2 |> ccmp
        Ty.compare t1 t2 |> ccmp
        Ty.compare t1' t2'
  end *)

  (* As in CDuce, we follow POPL'15 but keep constraint merged:
     - A Constraint Set C, is a (sorted list of triples) (s, α, t)
     - Adding a new constraint for an existing variable merges them.
     - If such a constraint is trivially unsatifiable (because s and
       t are both monomorphic and s ≤ t does not hold), then
       a failure is returned
  *)
  module CS(C: Constraint) = struct

    type t = [] | (::) of C.t * t
    let _of_list l =
      List.fold_left (fun acc c -> (c :: acc)) [] l

    let assert_sat s t =
      if C.VSet.subset (C.T.vars s) C.delta &&
         C.VSet.subset (C.T.vars t) C.delta &&
         not (C.T.leq s t)
      then raise_notrace Unsat
    let any = []
    let is_any t = (t = [])
    let singleton ((s, _, t) as e) = assert_sat s t; [e]
    let merge (s, v, t) (s', _, t') =
      let ss = C.T.cup s s' in
      let tt = C.T.cap t t' in
      assert_sat ss tt;
      (ss, v, tt)

    let rec add c l =
      match l with
        [] -> [ c ]
      | c' :: ll ->
        let n = C.vcompare c c' in
        if n < 0 then c::l
        else if n = 0 then (merge c c')::ll
        else c' :: add c ll

    let rec cap l1 l2 =
      match l1, l2 with
      | [],  _ -> l2
      | _, []  -> l1
      | c1::ll1, c2::ll2 ->
        let n = C.vcompare c1 c2 in
        if n < 0 then c1 :: cap ll1 l2
        else if n > 0 then c2 :: cap l1 ll2
        else (merge c1 c2)::cap ll1 ll2

    (* A constraint set l1 subsumes a constraint set l2 if
       forall constraint c2 in m2, there exists
       c1 in t1 such that c1 subsumes c2
    *)
    let rec subsumes l1 l2 =
      match l1, l2 with
      | _, [] -> true
      | [], _ -> false
      | c1::ll1, c2::ll2 ->
        let n = C.vcompare c1 c2 in
        if n < 0 then subsumes ll1 l2
        else if n > 0 then false
        else C.subsumes c1 c2 && subsumes ll1 ll2

    let rec compare l1 l2 =
      match l1, l2 with
        [], [] -> 0
      | [], _ -> -1
      | _, [] -> 1
      | c1 :: ll1, c2 :: ll2 ->
        let c = C.compare c1 c2 in
        if c <> 0 then c else compare ll1 ll2

    let rec to_list_map f = function
        [] -> List.[]
      | e :: ll -> (f e)::to_list_map f ll
  end

  module CSS = struct
    (* Constraint sets are records with each component representing 
       the subset of constraints of a given kind (type, field or row). They
       represent the conjunction of these constraints. *)

    module TCS = CS(TConstr)
    module FCS = CS(FConstr)
    (* module RCS = CS(RConstr) *)
    type constraint_set = {
      types : TCS.t;
      fields : FCS.t;
      (* rows  : RCS.t; *)
    }

    (* Sets of constraint sets are ordered list of non subsumable elements.
       They represent union of constraints, so we maintain the invariant
       that we don't want to add a constraint set that subsumes an already
       existing one.
    *)
    type t = constraint_set list
    let empty : t = []
    let is_empty = function [] -> true | _ -> false
    let any : t = [{ types = TCS.any; fields = FCS.any }]
    let is_any = function [{ types; fields }] when TCS.is_any types && FCS.is_any fields -> true | _ -> false
    let tsingleton e = [{ types = e; fields = [] }]
    let fsingleton e = [{ types = []; fields = e }]
    let tsingle e =
      try tsingleton (TCS.singleton e)
      with Unsat -> empty
    let fsingle e =
      try fsingleton (FCS.singleton e)
      with Unsat -> empty
    let rec insert_aux ({types;fields} as c) l =
      match l with
        [] -> [c]
      | {types=types';fields=fields'} as c' :: ll ->
        let n1 = TCS.compare types types' in
        let n2 = FCS.compare fields fields' in
        if n1 < 0 then c::l
        else if n1 = 0 then
          if n2 < 0 then c::l
          else if n2 = 0 then ll
          else c' :: insert_aux c ll
        else
           c' :: insert_aux c ll
    let add ({types;fields} as c) l =
      if (List.exists (fun {types=t;_} -> TCS.subsumes types t) l)
        && (List.exists (fun {types=_;fields=f} -> FCS.subsumes fields f) l)
      then l
      else
        List.filter (fun {types=types';fields=fields'} ->
          (TCS.subsumes types' types && FCS.subsumes fields' fields) |> not) l
        |> insert_aux c

    let cup t1 t2 = List.fold_left (fun acc cs -> add cs acc) t1 t2
    let cap t1 t2 =
      (cartesian_product t1 t2) |>
      List.fold_left (fun acc (cs1,cs2) ->
        try 
        let r = {
          types = TCS.cap cs1.types cs2.types;
          fields = FCS.cap cs1.fields cs2.fields;
        } in
        add r acc
        with Unsat -> acc)
      empty

    let cup_lazy t1 t2 =
      if is_any t1 then any
      else cup t1 (t2 ())
    let cap_lazy t1 t2 =
      if is_empty t1 then empty
      else cap t1 (t2 ())

    let map_disj f t =
      List.fold_left (fun acc e -> cup_lazy acc (fun () ->  f e)) empty t
    let map_conj f t =
      List.fold_left (fun acc e -> cap_lazy acc (fun () -> f e)) any t
    let to_list l = l
  end

  module Toplevel = struct
    let to_ty e  = [ e ] |> VDescr.of_dnf |> Ty.of_def

    let pos_var v e = (Ty.empty, v, Ty.neg (to_ty e))

    let neg_var v e = (to_ty e, v, Ty.any)

    (* Extract a constraint for the smallest polymorphic (not in delta) top-level variable of a summand *)
    let extract_smallest (pvs, nvs, d) =
      let rec find_min_var acc o_min l =
        match l, o_min with
        | [], None -> None
        | [], Some v -> Some (v, acc)
        | v :: ll, _ when VarSet.mem v VS.delta.v -> find_min_var (v::acc) o_min ll
        | v :: ll, None -> find_min_var acc (Some v) ll
        | v :: ll, Some v_min ->
          if VS.tcompare v v_min < 0 then
            find_min_var (v_min::acc) (Some v) ll
          else find_min_var (v :: acc) o_min ll
      in
      match find_min_var [] None pvs, find_min_var [] None nvs with
        None, None -> None
      | Some (v, rem_pos), None -> Some (pos_var v (rem_pos, nvs, d))
      | None, Some (v, rem_neg) -> Some (neg_var v (pvs, rem_neg, d))
      | Some (vp, rem_pos), Some (vn, rem_neg) ->
        if VS.tcompare vp vn < 0 then
          Some (pos_var vp (rem_pos, nvs, d))
        else
          Some (neg_var vn (pvs, rem_neg, d))

  end

  module FToplevel = struct
    let to_oty e = [ e ] |> Ty.F.of_dnf

    let pos_var v e = (Ty.F.empty, v, Ty.F.neg (to_oty e))

    let neg_var v e = (to_oty e, v, Ty.F.any)

    (* Extract a constraint for the smallest polymorphic (not in delta) field variable of a summand *)
    let extract_smallest (pvs, nvs, d) =
      let rec find_min_var acc o_min l =
        match l, o_min with
        | [], None -> None
        | [], Some v -> Some (v, acc)
        | v :: ll, _ when FieldVarSet.mem v VS.delta.f ->
          find_min_var (v::acc) o_min ll
        | v :: ll, None -> find_min_var acc (Some v) ll
        | v :: ll, Some v_min ->
          if VS.fcompare v v_min < 0 then
            find_min_var (v_min::acc) (Some v) ll
          else find_min_var (v :: acc) o_min ll
      in
      match find_min_var [] None pvs, find_min_var [] None nvs with
        None, None -> None
      | Some (v, rem_pos), None -> Some (pos_var v (rem_pos, nvs, d))
      | None, Some (v, rem_neg) -> Some (neg_var v (pvs, rem_neg, d))
      | Some (vp, rem_pos), Some (vn, rem_neg) ->
        if VS.fcompare vp vn < 0 then
          Some (pos_var vp (rem_pos, nvs, d))
        else
          Some (neg_var vn (pvs, rem_neg, d))
  end

  module VDHash = Hashtbl.Make(VDescr)
  let norm_tuple_gen ~any ~conj ~diff ~disjoint ~norm n (ps, ns) =
    (* Same algorithm as for subtyping tuples.
       We define it outside norm below so that its type can be
       generalized and we can apply it to different ~any/~conj/...
    *)
    let ps = mapn (fun () -> List.init n (fun _ -> any)) conj ps in
    let rec psi acc ss ts () =
      let cstr = ss |> CSS.map_disj norm in
      CSS.cup_lazy cstr (fun () ->
        match ts with
          [] -> CSS.empty
        | tt :: ts ->
          if List.exists2 disjoint ss tt then psi acc ss ts ()
          else fold_distribute_comb (fun acc ss ->
              CSS.cap_lazy acc (psi acc ss ts)) diff acc ss tt
      )
    in psi CSS.any ps ns ()
  let _norm_records_directly (ps, ns) =
    let rec new_psi r0 rvs ns =
      let open Records.Atom in
      let check_field r ns_rest l ty =
        let ty_r = Records.Atom.find l r in
        Ty.F.leq ty ty_r
        ||
        let updated_bindings =
          LabelMap.add l (Ty.F.cap ty (Ty.F.neg ty_r)) r0.bindings
        in
        new_psi {r0 with bindings = updated_bindings} rvs ns_rest
      in
      match ns with
      | [] -> false
      | r::ns_rest ->
        begin match r.Records.Atom.tail with
        | Open ->
            LabelMap.for_all (check_field r ns_rest) r0.bindings
        | v when Records.Tail.equal v r0.tail ->
            LabelMap.for_all (check_field r ns_rest) r0.bindings
        | RowVar v when RowVarSet.mem v rvs ->
            LabelMap.for_all (check_field r ns_rest) r0.bindings
        | _ -> new_psi r0 rvs ns_rest
        end
    in
    let normalize_ps ps d =
      let intersect_fields lbl =
        List.fold_left (fun acc a -> Ty.F.cap acc (Records.Atom.find lbl a))
          Ty.F.any ps
      in
      let pos_fields =
        LabelMap.Set.elements d |> List.map (fun lbl -> lbl, intersect_fields lbl)
        |> LabelMap.of_list
      in
      let rowvars, r_tail =
        List.fold_left
        (fun (rvs, t) r ->
          match r.Records.Atom.tail with
          | Open -> rvs, t
          | Closed -> rvs, Records.Tail.Closed
          | RowVar v -> RowVarSet.add v rvs, t)
        (RowVarSet.empty, Open)
        ps
      in
      {Records.Atom.bindings=pos_fields; tail=r_tail}, rowvars
    in
    let dom = List.fold_left
      (fun acc a -> LabelMap.Set.union acc (Records.Atom.dom a))
        LabelMap.Set.empty (ps @ ns)
    in
    let r0, rvs = normalize_ps ps dom in
    LabelMap.exists (fun _ y -> Ty.F.is_empty y) r0.bindings
    ||
    new_psi r0 rvs ns
  let norm t =
    let memo = VDHash.create 16 in
    let rec norm_ty t =
      let vd = Ty.def t in
      match VDHash.find_opt memo vd  with
      | Some cstr -> cstr
      | None ->
        VDHash.add memo vd CSS.any;
        let res =
          if Ty.is_empty t then CSS.any
          else if VarSet.subset (Ty.vars t) VS.delta.v then CSS.empty
          else vd |> VDescr.dnf |> CSS.map_conj norm_summand
        in
        VDHash.remove memo vd ; res
    and norm_summand summand =
      match Toplevel.extract_smallest summand with
      | None ->
        let (_,_,d) = summand in
        norm_descr d
      | Some cs -> CSS.tsingle cs
    and norm_descr d =
      let (cs, others) = d |> Descr.components in
      if others then CSS.empty
      else cs |> CSS.map_conj norm_comp
    and norm_comp c =
      let open Descr in
      match c with
      | Enums c -> norm_enums c
      | Arrows c -> norm_arrows c
      | Intervals c -> norm_intervals c
      | Tags c -> norm_tags c
      | Tuples c -> norm_tuples c
      | Records c -> norm_records c
    and norm_enums d =
      match Enums.destruct d with
      | true, [] -> CSS.any
      | _, _ -> CSS.empty
    and norm_intervals d =
      match Intervals.destruct d with
      | [] -> CSS.any
      | _ -> CSS.empty
    and norm_tags tag =
      let (cs, others) = tag |> Tags.components in
      if others then CSS.empty
      else cs |> CSS.map_conj norm_tagcomp
    and norm_tagcomp c =
      let tag = TagComp.tag c in
      c |> TagComp.dnf |> CSS.map_conj (norm_tag tag)      
    and norm_arrows arr =
      arr |> Arrows.dnf |> CSS.map_conj norm_arrow
    and norm_tuples tup =
      let (comps, others) = tup |> Tuples.components in
      if others then CSS.empty
      else comps |> CSS.map_conj norm_tuplecomp
    and norm_tuplecomp tup =
      let n = TupleComp.len tup in
      tup |> TupleComp.dnf |> CSS.map_conj (norm_tuple n)
    and norm_records r =
      r |> Records.dnf |> CSS.map_conj norm_record
    and norm_arrow (ps, ns) =
      let rec psi t1 t2 ps () =
        let cstr = CSS.cup_lazy (norm_ty t1) (fun () -> norm_ty t2) in
        let cstr_rec () = match ps with
            [] -> CSS.empty
          | (s1, s2) :: ps ->
            if Ty.disjoint t1 s1 || Ty.leq t2 s2 then psi t1 t2 ps ()
            else CSS.cap_lazy
              (psi (Ty.diff t1 s1) t2 ps ())
              (psi t1 (Ty.cap t2 s2) ps)
        in
        CSS.cup_lazy cstr cstr_rec
      in
      let norm_single_neg_arrow ps (t1, t2) =
        let cstr_domain = Ty.diff t1 (List.map fst ps |> Ty.disj) |> norm_ty in
        if CSS.is_empty cstr_domain then CSS.empty
        else
          let cstr_struct () =
            if List.is_empty ps then CSS.any else psi t1 (Ty.neg t2) ps () in
          CSS.cap_lazy cstr_domain cstr_struct
      in
      CSS.map_disj (norm_single_neg_arrow ps) ns
    and norm_tuple n line = norm_tuple_gen ~any:Ty.any ~conj:Ty.conj
        ~diff:Ty.diff ~disjoint:Ty.disjoint ~norm:norm_ty n line
    and norm_tag tag line =
      let tys = TagComp.line_emptiness_checks tag line in
      CSS.map_disj norm_ty tys
    and norm_record (ps, ns) =
      let line, n = Records.dnf_line_to_tuple (ps, ns) in
      let disjoint s1 s2 =
        let t = Ty.F.cap s1 s2 in
        Ty.F.is_required t && Ty.F.get t |> Ty.is_empty
      in
      norm_tuple_gen ~any:Ty.F.any ~conj:Ty.F.conj
        ~diff:Ty.F.diff ~disjoint ~norm:norm_oty n line
    and norm_oty oty =
      (* TODO here using Ty.F.is_empty might lead to an uncatched GetCache effect? *)
      let dnf = Ty.F.dnf oty in
      (* if List.is_empty dnf then CSS.any
      else *)
        if FieldVarSet.subset (Ty.F.fvars oty) VS.delta.f then
        let n, o = Ty.F.destruct oty in
        if o then CSS.empty else norm_ty n
      else dnf |> CSS.map_conj norm_fsummand

    and norm_fsummand summand =
      match FToplevel.extract_smallest summand with
      | None ->
        let (_, _, leaf) = summand in
        begin match leaf with
        | Ty.F.Absent -> CSS.empty
        | Ty.F.Ty t -> norm_ty t
        end
      | Some cs -> CSS.fsingle cs
    in
    (*  and norm_row *)
    norm_ty t

  let propagate cs =
    let memo_ty = VDHash.create 8 in
    let rec aux prev (cs : CSS.TCS.t) =
      match cs with
      | CSS.TCS.[] -> prev |> CSS.tsingleton
      | ((t',_, t) as constr) :: cs' ->
        let ty = Ty.diff t' t in
        if VDHash.mem memo_ty (Ty.def ty) then
          aux (CSS.TCS.add constr prev) cs'
        else
          let () = VDHash.add memo_ty (Ty.def ty) () in
          let css = norm ty in
          let css' () = cs |> CSS.TCS.cap prev |> CSS.tsingleton in
          let css = CSS.cap_lazy css css' in
          let res = css |> CSS.to_list |> CSS.map_disj (fun {CSS.types;_} -> (aux CSS.TCS.any types))  in
          VDHash.remove memo_ty (Ty.def ty); res
    in
    aux CSS.TCS.any cs

  let solve {CSS.types = cs; fields = _} =
    let renaming = ref Subst.identity in
    let to_eq (ty1, v, ty2) =
      let v' = Var.mk (Var.name v) in
      renaming := Subst.add v' (Ty.mk_var v) !renaming ;
      (v, Ty.cap (Ty.cup ty1 (Ty.mk_var v')) ty2)
    in
    let rec unify eqs =
      match eqs with
      | [] -> Subst.identity
      | (v,ty)::eqs ->
        let (_,ty') = Ty.of_eqs [v, ty] |> List.hd in
        let s = Subst.singleton v ty' in
        let eqs' = eqs |> List.map (fun (v,eq) -> (v, Subst.apply s eq)) in
        let res = unify eqs' in
        Subst.add v (Subst.apply res ty') res
    in
    cs |> CSS.TCS.to_list_map to_eq |> unify |> Subst.map (Subst.apply !renaming)

  let tally cs =
    let ncss = cs
      |> CSS.map_conj (fun (s,t) -> norm (Ty.diff s t)) in
    let mcss = ncss
      |> CSS.to_list |> CSS.map_disj (fun {CSS.types;_} -> propagate types) in
    mcss |> CSS.to_list |> List.map solve
end

let tally_with_order vs =
  let module Tallying = Make(val vs : VarSettings) in
  Tallying.tally

let tally delta cs =
    let vs = (module struct
    let tcompare = Var.compare
    let fcompare = FieldVar.compare
    let rcompare = RowVar.compare
    let delta = {v = delta; f = FieldVarSet.empty; r = RowVarSet.empty}
  end : VarSettings) in
  tally_with_order vs cs

let tally_with_priority preserve delta =
  let cnt = ref 0 in
  let pmap = List.fold_left
      (fun acc v -> cnt := !cnt + 1 ; VarMap.add v !cnt acc)
      VarMap.empty preserve
  in
  let cmp = (module struct
    let tcompare v1 v2 =
    match VarMap.find_opt v1 pmap, VarMap.find_opt v2 pmap with
    | None, None -> Var.compare v1 v2
    | Some _, None -> 1
    | None, Some _ -> -1
    | Some i1, Some i2 -> compare i2 i1
    let fcompare = FieldVar.compare
    let rcompare = RowVar.compare
    let delta = {v = delta; f = FieldVarSet.empty; r = RowVarSet.empty}
  end : VarSettings)
  in
  tally_with_order cmp

let decompose delta s1 s2 =
  let vars1 = VarSet.union (Subst.domain s1) (Subst.intro s1) in
  let vars2 = VarSet.union (Subst.domain s2) (Subst.intro s2) in
  let vars = VarSet.union vars1 vars2 in
  let fresh, fresh_inv = Subst.refresh (VarSet.diff vars delta) in
  let fresh_vars = Subst.intro fresh in
  let s2 = Subst.compose fresh s2 in
  let cs = VarSet.elements vars |> List.concat_map (fun v ->
      let t1, t2 = Subst.find s1 v, Subst.find s2 v in
      [ t1, t2 ; t2, t1 ]
    )
  in
  tally (VarSet.union delta fresh_vars) cs |> List.map
    (fun s -> Subst.compose fresh_inv s |> Subst.restrict vars)
